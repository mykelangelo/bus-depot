buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.gretty:gretty:2.3.1'
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.8.12"
    }
}
plugins {
    id 'org.unbroken-dome.test-sets' version '2.0.3'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.gretty'
apply plugin: 'docker-compose'

defaultTasks 'clean', 'build'

dockerCompose {
    useComposeFiles = ['docker-compose.yml']
    forceRecreate = true
}

sourceCompatibility = 11
targetCompatibility = 11

testSets {
    e2e { dirName = 'e2e' }
}

gretty {
    servletContainer = 'tomcat9'
    contextPath = ""
    springBoot = false
    fastReload
    integrationTestTask = 'e2e'
}

wrapper {
    gradleVersion = '5.2.1'
    distributionType = Wrapper.DistributionType.ALL
}

project.afterEvaluate {
    tasks.build.dependsOn tasks.e2e
}

task startDependencies {
    dependsOn 'composeDown'
    dependsOn 'composeUp'
}

task stage

configurations { stage {} }

task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.stage) {
        include "webapp-runner*"
    }
}

tasks.stage.dependsOn(copyToLib)

dependencies {
/* Apache Commons BEGIN */
    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation "org.apache.commons:commons-collections4:4.2"
    implementation "commons-io:commons-io:2.6"
/* Apache Commons END */

/* JEE dependencies BEGIN */
    implementation "javax.servlet:jstl:1.2"
    implementation "javax.servlet:javax.servlet-api:4.0.1"
/* JEE dependencies END */

/* Logging BEGIN */
    implementation "org.slf4j:slf4j-api:1.7.25"
    implementation "org.slf4j:jul-to-slf4j:1.7.25"
    implementation "ch.qos.logback:logback-classic:1.2.3"
/* Logging END */

/* Database Connectivity BEGIN */
    implementation "mysql:mysql-connector-java:8.0.12"
    implementation "com.zaxxer:HikariCP:3.1.0"
    implementation 'org.flywaydb:flyway-core:6.0.0-beta'
/* Database Connectivity END */

/* Password Hashing library BEGIN */
    implementation "org.mindrot:jbcrypt:0.4"
/* Password Hashing library END */

/* Unit testing BEGIN */
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.2"
    testImplementation "org.mockito:mockito-junit-jupiter:2.23.4"
    testImplementation "org.assertj:assertj-core:3.12.1"
    // For Repository tests
    testImplementation "com.wix:wix-embedded-mysql:4.2.0"
/* Unit testing END */

/* E2E testing BEGIN */
    testImplementation "org.seleniumhq.selenium:selenium-java:3.141.59"
    testImplementation "io.github.bonigarcia:webdrivermanager:3.1.1"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:3.141.59"
/* E2E testing END */

/* Heroku Gradle buildpack support BEGIN */
    stage 'com.github.jsimone:webapp-runner:9.0.16.0'
/* Heroku Gradle buildpack support END */
}
repositories {
    jcenter()
}
tasks.withType(Test) {
    useJUnitPlatform()
}